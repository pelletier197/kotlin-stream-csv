plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version" apply false

    id "org.jlleitschuh.gradle.ktlint" version "$ktlint_version"
    id "org.jlleitschuh.gradle.ktlint-idea" version "$ktlint_version"

    id 'com.github.kt3k.coveralls' version "$coveralls_version"

    id 'net.researchgate.release' version "$researchgate_release_version" apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group 'io.github.pelletier197'
    version "1.0.0-SNAPSHOT"

    sourceCompatibility = 11
    targetCompatibility = 11

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = true
        }
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
            events "standardOut", "passed", "skipped", "failed"
        }
        finalizedBy jacocoTestReport
    }
}

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    description = 'Generates an aggregate report from all subprojects'

    additionalSourceDirs.setFrom(files(subprojects.sourceSets.main.allSource.srcDirs))
    sourceDirectories.setFrom(files(subprojects.sourceSets.main.allSource.srcDirs))
    classDirectories.setFrom(files(subprojects.sourceSets.main.output))
    executionData.setFrom(files(subprojects.jacocoTestReport.executionData))

    reports {
        html.enabled = true
        xml.enabled = true
    }

    doFirst {
        executionData.setFrom(files(executionData.findAll { it.exists() }))
    }
}

coveralls {
    sourceDirs = ["${project(":core").projectDir}/src/main/kotlin"]
    service = "circleci"
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
    description = "Uploads coverage report to coveralls"
    dependsOn jacocoRootReport
}
